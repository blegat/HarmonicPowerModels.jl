clear
!2-bus Dy11 example. 


! adapted from:
! IEEE 4-bus test case   D-Y Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.2busDy11   basekV=12.47 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

! **** 3-PHASE STEP-DOWN TRANSFORMER 12.47/4.16 KV Delta-Ygrd
new transformer.t1 xhl=6
~ wdg=1 bus=sourcebus conn=delta kV=12.47 kVA=6000 %r=0.5 
~ wdg=2 bus=n3 conn=wye   kV=4.16  kVA=6000 %r=0.5 


New "Spectrum.Spectrum_1" NumHarm=8 CSVFile=Spectrum_Load_1.csv 
New "Spectrum.Spectrum_2" NumHarm=8 CSVFile=Spectrum_Load_2.csv

! **** WYE-CONNECTED 4.16 KV LOAD
new load.load1 phases=3 bus1=n3 conn=wye kV=4.16 kW=5400 pf=0.9  model=1 spectrum=spectrum_2
! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL
~ vminpu=0.75    ! model will remain const p,q down to 0.75 pu voltage    

!needed to get harmonics results out
New monitor.MPCC element=transformer.t1 mode=0


set voltagebases=[12.47, 4.16] 
calcvoltagebases     ! **** let DSS compute voltage bases
solve

!verify fundamental results
show voltages LN Nodes 




!Set harmonics solution mode now
set mode=harmonicT

!Calculate harmonics 
solve

!export harmonics results
export monitor MPCC
show monitor MPCC