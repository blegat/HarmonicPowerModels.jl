clear
!2-bus cable example. 


! adapted from:
! IEEE 4-bus test case   D-Y Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.2buscable   basekV=36 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

! line
! http://www.tim-kabel.hr/images/stories/katalog/datasheetHRV/0516_NA2XS(F)2Y_ENG.pdf
! external diam - 2x sheet = concentric neutral diameter
! external diameter = avg(51.4,55.5) = 53.5
! conductor diameter = 24.6mm
! concentric neutral diam = 53.5- 2 x 2.5 = 48.5
! insulation thickness = 8.8 mm
! diains = diameter + 2x inslayer = 24.6+ 2x8.8 = 42.2
New CNData.NA2XS(FL)2Y_400/35
~ Runits=km radunits=mm GMRunits=mm
~ InsLayer=8.8 DiaIns=42.2 DiaCable=53.5 !Cable
~ Rdc=0.0778  Diam=24.6 !Phase Conductor
~ Rstrand=0.4886 ! -> copper, 35mm
~ DiaStrand=1.0  k= 39 !Neutral 

!right next to eachother, i.e. 
New LineGeometry.NA2XS3x1x400/35 nconds=3 nphases=3 reduce=no
~ cond=1 CNCable=NA2XS(FL)2Y_400/35 x=0 h=0 units=m
~ cond=2 CNCable=NA2XS(FL)2Y_400/35 x=0.0535 h=0 units=m
~ cond=3 CNCable=NA2XS(FL)2Y_400/35 x=0.107 h=0 units=m

New line.section1 phases=3 bus1=sourcebus bus2=b1 geometry=NA2XS3x1x400/35  length=1500 units=m


New "Spectrum.Spectrum_1" NumHarm=8 CSVFile=Spectrum_Load_1.csv 
New "Spectrum.Spectrum_2" NumHarm=8 CSVFile=Spectrum_Load_2.csv

! **** WYE-CONNECTED 4.16 KV LOAD
new load.load1 phases=3 bus1=b1 conn=wye kV=4.16 kW=5400 pf=0.9  model=5 spectrum=spectrum_2
! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL
~ vminpu=0.75    ! model will remain const p,q down to 0.75 pu voltage    

!needed to get harmonics results out
New monitor.MPCC element=line.section1 mode=16


set voltagebases=[36] 
calcvoltagebases     ! **** let DSS compute voltage bases

!-----------------------
!solve fundamental
solve

!verify fundamental results
show voltages LN Nodes 


!-----------------------
!solve all harmonics

!Set harmonics solution mode now
set mode=harmonicT

!Calculate harmonics 
solve

!export harmonics results
export monitor MPCC
show monitor MPCC

show lineconstants

!-----------------------
!solve specific frequency
Solve mode=snapshot    ! (this is the default)
Solve Frequency=180
Sample  ! cause monitors to take a sample of the present solution

show monitor MPCC
